# Maintainer: Danct12 <danct12@disroot.org>
# Contributor: Brett Cornwall <ainola@archlinux.org>
# Contributor: Omar Pakker

pkgname=wlroots-hwc
pkgver=0.11.0
pkgrel=4
license=('MIT')
pkgdesc='Modular Wayland compositor library - with hwcomposer backend'
url='https://github.com/swaywm/wlroots'
arch=('armv7h' 'aarch64')
depends=(
    'libinput'
    'libxkbcommon'
    'opengl-driver'
    'pixman'
    'xcb-util-errors'
    'xcb-util-image'
    'xcb-util-wm'
    'libhybris'
)
makedepends=(
    'meson'
    'ninja'
    'wayland-protocols'
    'android-headers-9.0'
)
provides=(
    'libwlroots.so'
    'wlroots'
)
_commit="1b2661c5f4b6799377b45abe65d34806052da19a"
source=(
    "wlroots-$_commit.tar.gz::https://github.com/hybris-mobian/wlroots/archive/${_commit}.zip"
    '0001-keypad-slide.patch'
)
sha256sums=('0cf15f856f3d11e73b9399488aa88e0c396511939304f12fe03501e7e48bad09'
            'c1b41278072133f01995d0bdd1dba03a51deebf06c7dd73ff84db9df1512332b')

prepare() {
    cd "wlroots-$_commit"
    patch -p1 -N < ../0001-keypad-slide.patch
}

build() {
    meson "wlroots-$_commit" build \
        --prefix=/usr \
        --buildtype=plain \
        -Dlibcap=enabled \
        -Dlogind=enabled \
        -Dlogind-provider=systemd \
        -Dxcb-errors=enabled \
        -Dxcb-icccm=enabled \
        -Dxcb-xkb=enabled \
        -Dxwayland=enabled \
        -Dx11-backend=enabled
    ninja -C build
}

package() {
    DESTDIR="$pkgdir" ninja -C build install
    install -Dm644 "wlroots-$_commit/LICENSE" -t "$pkgdir/usr/share/licenses/$pkgname/"
}
