# Maintainer: Danct12 <danct12@disroot.org>
pkgname=phoc-hwc
pkgver=0.5.1
pkgrel=2
pkgdesc="A compositor for phones based on wlroots - with hwcomposer backend"
arch=('armv7h' 'aarch64')
url="https://source.puri.sm/Librem5/phoc"
license=('GPL')
provides=('phoc')
depends=('gobject-introspection' 'gnome-desktop' 'libinput' 'mutter'
         'xcb-util-errors' 'xcb-util-wm' 'wlroots-hwc')
makedepends=('meson' 'ninja')

_commit="0855780be54ded79fdfcb7f280439b478d4d2e7f"
source=(https://github.com/hybris-mobian/phoc/archive/${_commit}.tar.gz
	'206.patch'
	'0001-keypad-slide.patch'
	'0002-keypad-slide.patch')

prepare() {
	cd phoc-${_commit}
	patch -p1 -N < ../206.patch
	patch -p1 -N < ../0001-keypad-slide.patch
	patch -p1 -N < ../0002-keypad-slide.patch
}

build() {
	arch-meson phoc-${_commit} output -Dtests=false -Dembed-wlroots=disabled
	ninja -C output
}

package() {
	DESTDIR="$pkgdir" ninja -C output install

	# Install scale-to-fit helper
	install -Dm755 phoc-${_commit}/helpers/scale-to-fit "$pkgdir"/usr/bin/scale-to-fit
}

md5sums=('4301471c9397520981b56637dc2d5b3d'
         'f4f4f563293ddd0a6c992efb2db457d9'
         '5f9c520a4e33532a7efa02e8cf590ad5'
         '08574327f0952bdf95c0ddd058385c31')
